meta (
    description """
This is just a set of demos to demonstrate **RedBarrel** features.

If you want to see more demos, send me a mail.

""",
    title "RedBarrel Demos"

);


root /here;

global config file:config.ini;
global app python:redbarrel.demos.misc.app;

path hello_world (
    description "Simplest application: Hello World!",
    method GET,
    url /,
    use python:redbarrel.demos.misc.hello
);

path counter (
    description "A counter with a persistent state on the server.",
    method GET,
    url /count,
    use python:redbarrel.demos.misc.counter
);

path counter2 (
    description """
    A counter with a persistent state on the server,
    using 2 globals:

     - a config file
     - an app that load the config file
    """,
    method GET,
    url /count2,
    use python:redbarrel.demos.misc.counter2
);


path index_html (
    description "A simple html page",
    method GET,
    url /index.html,
    use python:redbarrel.demos.misc.html,
    response-headers (
        set content-type "text/html"
    )
);

path capitalize (
    description "A web service with several post/pre processing",

    method POST,
    url /capitalize,
    use python:redbarrel.demos.misc.capitalize,

    request-description "blah",

    response-status (
        describe 200 "Success",
        describe 400 "The request is probably malformed",
        describe 401 "Authentication failure"
    ),

    request-body (
        description "Send a string in json and the server returns it Capitalized.",
        unless type is json return 400
    ),

    request-headers (
        unless Authorization validates with python:redbarrel.demos.misc.auth return 401
    ),

    response-headers (
        set content-type "application/json",
    ),

    response-body (
        description "The string, Capitalized !",
        unless type is json return 503
    )
);

path css (
    description "Serving a static CSS file",
    method GET,
    url /redbarrel.css,
    # the location is absolute or relative to the rbr file
    use file:../../media/redbarrel.css,
    response-headers (
        set content-type "text/css"
    )
);

path compressed_css (
    description "Serving a static CSS file",
    method GET,
    url /redbarrel-small.css,
    use file:../../media/redbarrel.css,
    response-headers (
        set content-type "text/css"
    ),

    response-body (
        description "The CSS is compressed using cssmin",
        alter with python:redbarrel.demos.misc.compress_css
    )

);

# demonstrates a custom type
type blob python:redbarrel.demos.misc.blob;

path blob (
    description "A web service with a custom type",
    method POST,
    url /blob,
    use python:redbarrel.demos.misc.blobish,
    request-body (
        unless type is blob return 400
    )
);

#path here (
#    use proxy:http://localhost:5000,
#    description "Proxied",
#    url /proxy
#);

path multi (
    description "Allow multiple methods",
    method PUT|GET|POST,
    url /index2.html,
    use python:redbarrel.demos.misc.html,
    response-headers (
        set content-type "text/html"
    )
);

path dummy (
    description "Uses a Dummy function by default",
    url /dummy
);
